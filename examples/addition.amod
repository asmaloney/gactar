==model==

// The name of the model (used when generating code and for error messages)
name: addition

// Description of the model (currently output as a comment in the python code)
description: 'This is a model which adds numbers. From the u1_addition.py tutorial.'

// Examples of starting goals to use when running the model
examples {
    "add 3 1 count:None sum:None"
    "add 5 2 count:None sum:None"
}

==config==

// Turn on logging by setting 'log' to 'true' or 1
actr { log: false }

// List of buffers to create by name
buffers { goal, retrieve }

// Memories to create
memories {
    memory {
        // Attach this buffer by name
        buffer: retrieve
    }
}

==init==

// Initialize the memory named "memory"
memory {
    'count 0 1'
    'count 1 2'
    'count 2 3'
    'count 3 4'
    'count 4 5'
    'count 5 6'
    'count 6 7'
    'count 7 8'
}

==productions==

// Name of the production
initializeAddition {
    // Buffers to match
    match {
        goal: 'add ?num1 ?num2 count:None?count sum:None?sum'
    }
    // Steps to execute
    do {
        set field count of goal to 0
        set field sum of goal to num1
        recall 'count ?num1 ?next' from memory
    }
}

incrementSum {
    match {
        goal: 'add ?num1 ?num2 count:?count!?num2 sum:?sum'
        retrieve: 'count ?sum ?next'
    }
    do {
        set field sum of goal to next
        recall 'count ?count ?n2' from memory
    }
}

incrementCount {
    match {
        goal: 'add ?num1 ?num2 count:?count sum:?sum'
        retrieve: 'count ?count ?next'
    }
    do {
        set field count of goal to next
        recall 'count ?sum ?n2' from memory
   }
}

terminateAddition {
    match {
        goal: 'add ?num1 ?num2 count:?num2 sum:?sum'
    }
    do {
        set goal to 'result ?sum'
        print sum
    }
}