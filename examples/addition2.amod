==model==

// The name of the model (used when generating code and for error messages)
name: addition2

// Description of the model (currently output as a comment in the python code)
description: "This model is based on the u1_addition2.py tutorial."

// Examples of starting goals to use when running the model
examples {
    'add 3 6 4 7 None None None'
}

==config==

actr { log: false }

// List of buffers to create by name
buffers { goal, retrieve }

// Memories to create
memories {
    memory {
        // Attach this buffer by name
        buffer: retrieve
    }
}

==init==

// Initialize the memory named "memory"
memory {
    'addfact 3 4 7'
    'addfact 6 7 13'
    'addfact 10 3 13'
    'addfact 1 7 8'
}

==productions==

// Name of the production
startPair {
    // Buffers to match
    match {
        goal: `add ? ?one1 ? ?one2 ? None?ans ?`
    }
    // Steps to execute
    do {
        set field 6 of goal to 'busy'
        recall `addfact ?one1 ?one2 ?` from memory
    }
}

addOnes {
    match {
        goal: `add ? ? ? ? ? busy?ans ?carry`
        retrieve: `addfact ? ? ?sum`
    }
    do {
        set field 6 of goal to sum
        set field 7 of goal to 'busy'
        recall `addfact 10 ? ?sum` from memory
    }
}

processCarry {
    match {
        goal: `add ?ten1 ? ?ten2 ? None?tenAns ?oneAns busy?carry`
        retrieve: `addfact 10 ?rem ?sum`
    }
    do {
        set field 5 of goal to 'busy'
        set field 6 of goal to rem
        set field 7 of goal to 1
        recall `addfact ?ten1 ?ten2 ?` from memory
    }
}

noCarry {
    match {
        goal: `add ?ten1 ? ?ten2 None?tenAns ?oneAns busy?carry`
        memory: `error:True`
    }
    do {
        set field 4 of goal to 'busy'
        set field 6 of goal to 0
        recall `addfact ?ten1 ?ten2 ?` from memory
    }
}

addTensDone {
    match {
        goal: `add ? ? ? ? busy?tenAns ?oneAns 0`
        retrieve: `addfact ? ? ?sum`
    }
    do {
        print sum, oneAns
        set field 5 of goal to sum
    }
}

addTensCarry {
    match {
        goal: `add ? ? ? ? busy?tenAns ? 1?carry`
        retrieve: `addfact ? ? ?sum`
    }
    do {
        set field 7 of goal to 0
        recall `addfact 1 ?sum ?` from memory
    }
}