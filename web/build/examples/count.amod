==model==

// The name of the model (used when generating code and for error messages)
name: count

// Description of the model (currently output as a comment in the python code)
description: "This is a model which adds numbers. Based on the u1_count.py tutorial."

// Examples of starting goals to use when running the model
examples {
    "countFrom 2 5 starting"
    "countFrom 1 7 starting"
}

==config==

// Turn on logging by setting 'log' to 'true' or 1
actr { log: false }

// List of buffers to create by name
buffers { goal, retrieve }

// Memories to create
memories {
    memory {
        // Attach this buffer by name
        buffer: retrieve
    }
}

==init==

// Initialize the memory named "memory"
memory {
    'count 0 1'
    'count 1 2'
    'count 2 3'
    'count 3 4'
    'count 4 5'
    'count 5 6'
    'count 6 7'
    'count 7 8'
    'count 8 9'
    'count 9 10'
}

==productions==

// Name of the production
start {
    // Buffers to match
    match {
        goal: `countFrom ?start ?end starting`
    }
    // Steps to execute
    do {
        recall `count ?start ?` from memory
        set goal to `countFrom ?start ?end counting`
    }
}

increment {
    match {
        goal: `countFrom ?x !?x counting`
        retrieve: `count ?x ?next`
    }
    do {
        print x
        recall `count ?next ?` from memory
        set field 1 of goal to next
    }
}

stop {
    match {
        goal: `countFrom ?x ?x counting`
    }
    do {
        print x
        clear goal
    }
}